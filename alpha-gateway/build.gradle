/*
 * This build file was auto generated by running the Gradle 'init' task
 * by '100041' at '19/8/16 4:58 PM' with Gradle 2.10
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.10/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.2"
    }
}

plugins {
    id 'java' // or 'groovy' Must be explicitly applied
    id 'com.github.johnrengelman.shadow' version "1.2.2"
}

def localBuildFile = "${projectDir}/build-local.gradle"
def useLocalBuild = new File(localBuildFile).exists()
if (useLocalBuild) {
    apply from : localBuildFile
}

defaultTasks 'shadowJar'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

// Define a version registry so that we can avoid hard coding of the version numbers
// Note that this applies only to dependencies and not to the plugins and build
ext {
    vertxVersion = '3.2.1'
    slf4jVersion = '1.7.12'
    junitVersion = '4.12'
    logbackVersion = '1.0.13'
}

version = "0.1-snapshot"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    // Vertx specific dependencies
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    compile "io.vertx:vertx-hazelcast:${vertxVersion}"
    compile "io.vertx:vertx-dropwizard-metrics:${vertxVersion}"

    // Other dependencies
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    // Test dependencies
    testCompile "junit:junit:${junitVersion}"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'org.alpha.bootstrap.ServerVerticle'
        attributes 'Created-By': 'Daniel Sample'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}